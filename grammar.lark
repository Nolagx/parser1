// TODO put regex recursive lists inside new rules to make them easier to parse

start: (_NEWLINE)* (statement (_NEWLINE)+)* (statement)?

?statement: relation_declaration
          | fact
          | rule
          | query
          | assign_string
          | assign_span
          | assign_var

assign_string: var_name "=" string -> assign_literal_string
             | var_name "=" "read" "(" string ")" -> assign_string_from_file_string_param
             | var_name "=" "read" "(" var_name ")" -> assign_string_from_file_var_param

assign_span: var_name "=" span

assign_var: var_name "=" var_name

relation_declaration: "new" _separator relation_name "(" decl_term ("," decl_term)* ")"

?decl_term: "str" -> decl_string
          | "spn" -> decl_span

rule: rule_head "<-" rule_body

rule_head: relation_name "(" _free_var_name_list ")"

rule_body: rule_body_relation ("," rule_body_relation)*

?rule_body_relation: relation
                   | ie_relation

query: "?" relation

relation: relation_name "(" term ("," term)* ")"

?term: span
     | string
     | var_name
     | free_var_name

fact: relation_name "(" fact_term ("," fact_term)* ")"

?fact_term: span
          | string
          | var_name

// TODO show free_var_name_list?
ie_relation: function_name "<" var_name ">" "(" _free_var_name_list ")" -> func_ie_relation
           | "extract" _separator "RGX" "<" string ">" "(" _free_var_name_list ")" "from" _separator var_name -> rgx_ie_relation

span: "[" int "," int ")"

_separator: (_WS_INLINE | _NEWLINE)+

int: INT -> integer

// TODO visit all string_lists and turn them into string
string: STRING -> string
       | STRING ((_LINE_OVERFLOW_ESCAPE)* STRING)+  -> string

_free_var_name_list: free_var_name ("," free_var_name)*

relation_name: LOWER_CASE_NAME
             | UPPER_CASE_NAME

function_name: LOWER_CASE_NAME
             | UPPER_CASE_NAME

var_name: LOWER_CASE_NAME

free_var_name : UPPER_CASE_NAME


LOWER_CASE_NAME: ("_"|LCASE_LETTER) ("_"|LETTER|DIGIT)*
UPPER_CASE_NAME: UCASE_LETTER ("_"|LETTER|DIGIT)*

_COMMENT: "#" /[^\n]*/

_LINE_OVERFLOW_ESCAPE: "\\" _NEWLINE

// CNAME: ("_"|LETTER) ("_"|LETTER|DIGIT)*
// common definitions: https://github.com/lark-parser/lark/blob/master/lark/grammars/common.lark

_NEWLINE: CR? LF
CR : /\r/
LF : /\n/


%import common.LCASE_LETTER
%import common.UCASE_LETTER
%import common.LETTER
%import common.DIGIT
%import common.WS_INLINE -> _WS_INLINE
%ignore _WS_INLINE
%import common.ESCAPED_STRING -> STRING
%import common.INT -> INT
%ignore _LINE_OVERFLOW_ESCAPE
%ignore _COMMENT